subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	sourceCompatibility = '1.6'
	
	configurations {
		provided
	}

	sourceSets {
		main {
			compileClasspath += configurations.provided
		}
		
		test {
			compileClasspath += configurations.provided
		}
	}

	eclipse.classpath.plusConfigurations += configurations.provided

	// We want a jar-with-dependencies so that all of our library classes show up when running
	// a hadoop job.
	// The other alternative is to list all of your needed libraries in -libjars
	jar {
		from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
	}
	
	repositories {
		mavenCentral()
		maven {
			url 'https://repository.cloudera.com/artifactory/cloudera-repos/'
		}
	}
	
	dependencies {
		compile "log4j:log4j:1.2.17"
		compile "joda-time:joda-time:2.2"
		compile "com.google.code.gson:gson:2.2.4"
		
		provided "commons-configuration:commons-configuration:1.6"
		provided "org.apache.hadoop:hadoop-common:$hadoopVersion"
		provided "org.apache.hadoop:hadoop-client:$hadoopVersion"
		
		compile "org.apache.crunch:crunch-core:$crunchVersion"
		compile "org.kitesdk:kite-data-core:$kiteVersion"
		compile "org.kitesdk:kite-data-crunch:$kiteVersion"
		compile "org.kitesdk:kite-data-hcatalog:$kiteVersion"
		
		testCompile "org.mockito:mockito-all:1.9.5"
		testCompile "junit:junit:4.11"
		testCompile "org.apache.mrunit:mrunit:1.0.0:hadoop2"
	}
}

task createWrapper(type: Wrapper) {
	gradleVersion = '1.11'
}
